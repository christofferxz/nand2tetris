// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // load = reset || load || inc
    // in = reset = load = inc
    Register(in= resetValue, load= selectorResult, out= registerValue, out= out);

    // Inc
    Inc16(in= registerValue, out= registerValuePlus);
    Mux16(a= registerValue, b= registerValuePlus, sel= inc, out= incValue);

    // Load
    Mux16(a= incValue, b= in, sel= load, out= loadValue);

    // Reset
    Mux16(a= loadValue, b= false, sel= reset, out= resetValue);

    // load do register
    Or(a= load, b= inc, out= loadOrinc);
    Or(a= loadOrinc, b= reset, out= selectorResult);
}