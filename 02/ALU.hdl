// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    
    // zx and nx
    Mux16(a= x, b= false, sel= zx, out= zxResult);

    Not16(in= zxResult, out= notX);
    Mux16(a= zxResult, b= notX, sel= nx, out= zxnxResult);

    // zy and ny
    Mux16(a= y, b= false, sel= zy, out= zyResult);

    Not16(in= zyResult, out= notY);
    Mux16(a= zyResult, b= notY, sel= ny, out= zynyResult);

    // f
    Add16(a = zxnxResult, b = zynyResult, out = addzxResult);
    And16(a= zxnxResult, b= zynyResult, out= andzxResult);
    
    Mux16(a= andzxResult, b= addzxResult, sel= f, out= xyResult);

    // no
    Not16(in= xyResult, out= notxyResult);
    Mux16(a= xyResult, b= notxyResult, sel= no, out= out, out[15] = firstOut, out[0..7]= leftOut, out[8..15] = rightOut);
    
    // zr
    Or8Way(in= leftOut, out= orLeftOut);
    Or8Way(in= rightOut, out= orRightOut);

    Not(in= orLeftOut, out= notOrLeftOut);
    Not(in= orRightOut, out= notOrRightOut);

    And(a= notOrLeftOut, b= notOrRightOut, out= zr);

    // ng
    And(a= firstOut, b= true, out= ng);
}